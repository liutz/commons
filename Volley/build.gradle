buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.library'
// 这个version是区分library版本的，因此当我们需要更新library时记得修改这个version
version = "1.0.6"


task clearJar(type: Delete) {
    delete 'libs/volley.jar'
}

task makeJar(type:org.gradle.api.tasks.bundling.Jar) {
    //指定生成的jar名
    baseName 'volley'
    //从哪里打包class文件
    from('build/intermediates/classes/release/com/android/volley/')
    //打包到jar后的目录结构 
    into('com/android/volley/')
    //去掉不需要打包的目录和文件 
    exclude('release/', 'BuildConfig.class', 'R.class')
    //去掉R$开头的文件 
    exclude{ it.name.startsWith('R$');}
}

makeJar.dependsOn(clearJar, build)

android {

    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    resourcePrefix "android-"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 21
        versionCode 1
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    repositories {
        flatDir {
            dirs 'libs' //this way we can find the .aar file in libs folder
        }
    }

    android {
        lintOptions {
            abortOnError false
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true // 支持author标记
        version true // 支持version标记
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}


dependencies {
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.google.code.gson:gson:2.2.4'
}
